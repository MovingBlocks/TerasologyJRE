/*
 * Copyright 2019 MovingBlocks
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'base'
    id 'maven-publish'
    id 'de.undercouch.download' version '3.4.3'
}

group = 'org.terasology.jre'
version = '0.0.1-SNAPSHOT'

// Uses Bellsoft Liberica JRE
def jreUrlBase = 'https://download.bell-sw.com/java/8u212/bellsoft-jre8u212'
def jreUrlFilenames = [
        Linux64   : 'linux-amd64.tar.gz',
        Linux32   : 'linux-i586.tar.gz',
        Windows64 : 'windows-amd64.zip',
        Windows32 : 'windows-i586.zip',
        Mac       : 'macos-amd64.zip'
]

task downloadJreAll {
    group 'Download'
    description 'Downloads JRE for all platforms'
}

jreUrlFilenames.each { os, file ->
    def jreTask = "downloadJre$os"
    def jreZip  = "$projectDir/jre/${os.toLowerCase()}.zip"
    
    task "$jreTask"(type: Download) {
        group 'Download'
        description "Downloads JRE for $os"

        src "$jreUrlBase-$file"
        dest jreZip
        overwrite false
    }
    
    downloadJreAll.dependsOn tasks.named(jreTask).get()
    
    publishing.publications {
        "jre$os"(MavenPublication) {
            artifact(jreZip) {
                builtBy tasks.named(jreTask).get()
            }
            artifactId os.toLowerCase()
        }
    }
}

publishing {
    repositories {
        maven {
            if (project.hasProperty('artifactoryUser') && project.hasProperty('artifactoryPass')) {
                credentials {
                    username artifactoryUser
                    password artifactoryPass
                }
            }
            url "http://artifactory.terasology.org/artifactory/libs-release-local"
        }
    }
}
